// Blocked bookings - LDU not active
traces
| where cloud_RoleName == "licences"
| where message startswith "Blocking case for booking:"
| where message !contains "staff"
| project timestamp, 
    PROBATION_AREA=extract("Probation Area: '(.*)'.*$", 1, message), 
    LDU=extract("Ldu: '(.*?)'.*$", 1, message),
    BOOKING_ID=extract("booking: '(\\d+)'.*$", 1, message)
| order by timestamp asc

// Blocked bookings - Staff not allocated
traces
| where cloud_RoleName == "licences"
| where message startswith "Blocking case for booking:"
| where message contains "staff"
| project NotAllocated=extract('staff:.*$', 0, message)
| distinct NotAllocated
| sort by NotAllocated asc

// Offender not present in Delius
traces
| where cloud_RoleName == 'licences'
| where message startswith "Offender not present in delius"
| project message
| distinct message

// Staff member not found in Delius
traces
| where cloud_RoleName == 'licences'
| where message startswith "Staff member not found in delius"
| project message
| distinct message

// Notifications per day, grouped by Kind
traces
| where cloud_RoleName == 'licences'
| where message == "AUDIT"
| where customDimensions.key == 'NOTIFY'
| summarize count() by NotifyTime=bin(timestamp, 1d),Kind=extract('^POST /hdc/send/([a-zA-Z]+)/.*$', 1, operation_Name )
| project NotifyTime , Notifications=count_, Kind
| order by NotifyTime

// RO role assignments
traces
| where cloud_RoleName == "licences"
| where message contains "Role"
| project message, timestamp
| order by timestamp

// delius users who are accessing the system but don't have the required roles
traces
| where cloud_RoleName == 'licences'
| where message  contains 'no acceptable role'
| where severityLevel  > 1 
| project username=extract('Sign in error for user: \'(.*)\' Error: Login error - no acceptable role', 1, message)
| distinct username

// Prison to Prison
customEvents
| where cloud_RoleName == 'licences' 
| where customDimensions.source_type == 'prison' and customDimensions.target_type == 'prison'
| where name == 'CaseHandover'
| project timestamp=format_datetime(timestamp, 'dd/MM/yyyy HH:mm') , bookingId=customDimensions.bookingId, source=customDimensions.source_agencyId, target=customDimensions.target_agencyId, type= customDimensions.transitionType
| order by timestamp asc

// Prison to Probation
customEvents
| where cloud_RoleName == 'licences' 
| where customDimensions.source_type == 'prison' and customDimensions.target_type == 'probation'
| where name == 'CaseHandover'
| project timestamp=format_datetime(timestamp, 'dd/MM/yyyy HH:mm') , bookingId=customDimensions.bookingId, sourcePrison=customDimensions.source_agencyId, targetProbationArea=customDimensions.target_probationAreaCode, type= customDimensions.transitionType
| order by timestamp asc


// Probation area to prison area
customEvents
| where cloud_RoleName == 'licences' 
| where customDimensions.source_type == 'probation' and customDimensions.target_type == 'prison'
| where name == 'CaseHandover'
| project timestamp=format_datetime(timestamp, 'dd/MM/yyyy HH:mm') , bookingId=customDimensions.bookingId, sourceProbationArea=customDimensions.source_probationAreaCode, sourceLdu=customDimensions.source_lduCode, targetPrison=customDimensions.target_agencyId,type= customDimensions.transitionType
| order by timestamp asc


// Prison sent/received
let target_agencies = customEvents
| where cloud_RoleName == 'licences' 
| where name == 'CaseHandover'
| where customDimensions.target_type == 'prison'
| summarize count() by agency = tostring(customDimensions.target_agencyId)
| project agency, target=count_;

let source_agencies = customEvents
| where cloud_RoleName == 'licences' 
| where name == 'CaseHandover'
| where customDimensions.source_type == 'prison'
| summarize count() by agency = tostring(customDimensions.source_agencyId)
| project agency, source=count_;

target_agencies 
| join kind=fullouter source_agencies on agency
| project agency=coalesce(agency, agency1) , sent=source, received=target;

// Probation areas sent/received
let target = customEvents
| where cloud_RoleName == 'licences' 
| where name == 'CaseHandover'
| where customDimensions.target_type == 'probation'
| summarize count() by ldu = tostring(strcat_delim(' - ', customDimensions.target_probationAreaCode, customDimensions.target_lduCode))
| project ldu, target=count_;

let source = customEvents
| where cloud_RoleName == 'licences' 
| where name == 'CaseHandover'
| where customDimensions.source_type == 'probation'
| summarize count() by ldu = tostring(strcat_delim(' - ', customDimensions.source_probationAreaCode, customDimensions.source_lduCode))
| project ldu, source=count_;

target 
| join kind=fullouter source on ldu
| project ldu=coalesce(ldu, ldu1) , sent=source, received=target;

// Prisons sent to the north west
customEvents
| where cloud_RoleName == 'licences' 
| where name == 'CaseHandover'
| where customDimensions.source_type == 'prison'
| where customDimensions.target_type == 'probation'
| where customDimensions.target_probationAreaCode in ('N01', 'C02', 'C06', 'C07')
| summarize count() by agency = tostring(customDimensions.source_agencyId)
| project agency, count=count_;

// LDUs sending cases in the north west
customEvents
| where cloud_RoleName == 'licences' 
| where name == 'CaseHandover'
| where customDimensions.source_type == 'probation'
| where customDimensions.source_probationAreaCode in ('N01', 'C02', 'C06', 'C07')
| summarize count() by ldu = tostring(strcat_delim(' - ', customDimensions.source_probationAreaCode, customDimensions.source_lduCode))
| project ldu, target=count_;

// New LDUS this week:
let lastweek = customEvents
| where cloud_RoleName == 'licences' 
| where name == 'CaseHandover'
| where customDimensions.target_type == 'probation'
| where customDimensions.target_probationAreaCode in ('N01', 'C02', 'C06', 'C07', 'N24')
| where timestamp > startofday(datetime("2020-02-24")) and timestamp < endofday(datetime("2020-02-28"))
| summarize count() by ldu = tostring(strcat_delim(' - ', customDimensions.target_probationAreaCode, customDimensions.target_lduCode))
| project ldu, target=count_;

let thisweek = customEvents
| where cloud_RoleName == 'licences' 
| where name == 'CaseHandover'
| where customDimensions.target_type == 'probation'
| where customDimensions.target_probationAreaCode in ('N01', 'C02', 'C06', 'C07', 'N24')
| where timestamp > startofday(datetime("2020-03-02"))
| summarize count() by ldu = tostring(strcat_delim(' - ', customDimensions.target_probationAreaCode, customDimensions.target_lduCode))
| project ldu, target=count_;


lastweek | join kind=rightanti thisweek on ldu;